## Includes
\inc "stdlib/bool.csaw"
\inc "stdlib/io.csaw"
\inc "stdlib/math.csaw"
\inc "stdlib/random.csaw"

\inc "random.csaw"

@strcmp(a: int8*, b: int8*): int32;
@(==)(a: int8*, b: int8*): int1 = strcmp(a, b) == 0;
@(!=)(a: int8*, b: int8*): int1 = strcmp(a, b) != 0;
@atoi(x: int8*): int32;

## Main - Ghost Game
@main::int32 {

    printf("Ghost Game\n\n");

    still_running: bool = true;
    score: int32 = 0;
	
	door_with_ghost: int32;
	door: int8*;
	door_num: int32;

    while (still_running) {
        door_with_ghost = randomInt(1, 3);
        printf("There are three doors.\nThere's a ghost behind one of them.\nWhich one do you open?\n");
        
		door = "";
        while (door != "1" && door != "2" && door != "3")
            door = readf("1, 2 or 3? ");
        
        door_num = atoi(door);
        if (door_num == door_with_ghost) {
            printf("GHOST!\n");
            still_running = false;
        } else {
            printf("No one there!\nYou can go one room further.\n");
            ++score;
        }
    }

    printf("Run!\nGame over! Your score: %d\n", score);
    ret 0;
}
